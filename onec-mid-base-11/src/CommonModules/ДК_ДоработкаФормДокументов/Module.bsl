#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт	
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьКомандуПересчитатьСкидку(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровИУслуг.Форма.ФормаДокумента"
		ИЛИ ИмяФормы = "Документ.РеализацияТоваровИУслуг.Форма.ФормаДокумента" Тогда	
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента"
		ИЛИ ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоПередСуммойДокумента(Форма);
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти   


#Область СлужебныйПрограммныйИнтерфейс
 
Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
//В документы ЗаказПокупателя, ПоступлениеТоваровИУслуг, РеализацияТоваровИУслуг	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"),Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДК_КонтактноеЛицо";
КонецПроцедуры  

Процедура ВставитьПолеКонтактноеЛицоПередСуммойДокумента(Форма)
//В документы ОплатаОтПокупателя,ОплатаПоставщику	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),,Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДК_КонтактноеЛицо";
КонецПроцедуры 

Процедура ДобавитьКомандуПересчитатьСкидку(Форма)
//В документ ЗаказПокупателя
    Группа = Форма.Элементы.Добавить("ГруппаСкидки", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.ОтображатьЗаголовок = Ложь;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;

	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"),Форма.Элементы.ГруппаСкидки);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДК_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "КомандаПересчитатьТаблицу";
	
	КнопкаФормы = Форма.Элементы.Добавить("КомандаПересчитатьТаблицу", Тип("КнопкаФормы"),Форма.Элементы.ГруппаСкидки);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаФормы.Картинка = БиблиотекаКартинок.Обновить;
		
КонецПроцедуры 

#КонецОбласти